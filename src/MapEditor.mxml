<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx" 
					   creationComplete="init()">
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.controls.Tree;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
			
			import spark.components.Group;
			import spark.components.List;
			import spark.components.VScrollBar;
			
			import bgedit.BgEditView;
			import bgedit.BgModel;
			
			import excel.ExcelReader;
			
			import manager.EventManager;
			import manager.EventType;
			import manager.GameEvent;
			import manager.ServerManager;
			
			
			public var fView:FormationsView = null;
			public var matsView:MatsView = null;
			
			private function init():void
			{
				_instance = this;
				
				var menu:ContextMenu = new ContextMenu;
				var item:ContextMenuItem = new ContextMenuItem("新建");
				item.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, onCreateNew);
				var item2:ContextMenuItem = new ContextMenuItem("重命名");
				item2.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, onRename);
				var item3:ContextMenuItem = new ContextMenuItem("删除");
				item3.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, onDelete);
				menu.addItem(item);
				menu.addItem(item2);
				menu.addItem(item3);
				accordion.contextMenu = menu;
				
				Data.getInstance().addEventListener(Event.COMPLETE, onDataReady);
				Data.getInstance().init();
				
				EventManager.getInstance().addEventListener(EventType.INIT_BG_DATA_COMPLETE, onBgDataInit);
				BgModel.getInstance().init();
				
				this.addEventListener(Event.ADDED_TO_STAGE, onAddToStage);
			}
			
			private function onAddToStage(e:Event):void
			{
				stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown);
			} 
			private function onKeyDown(e:KeyboardEvent):void
			{
				var code = e.keyCode;
				if(code == Keyboard.S && e.ctrlKey)
					saveFile(null);
			}
			
			
			private function onDataReady(e:Event):void
			{
				_tree = new Tree();
				_tree.selectedIndex = 0;
				_tree.dataProvider = Data.getInstance().levelXML;
				_tree.enabled = true;
				_tree.labelField = "@label";
				_tree.percentWidth = 100;
				_tree.percentHeight = 100;
				_tree.showRoot = false;
				_tree.addEventListener(MouseEvent.CLICK, onLevelClick);
				enemyEdit.addElement(_tree);
				
				_enemyEditView = new EditView(this, middleArea);
				_enemyEditView.init(Data.getInstance().levelXML.level[0].@label);
				
				var modelView:Panel = new Panel();
				modelView.percentHeight = 40;
				modelView.percentWidth = 100;
				modelView.title = "模型";
				var hgroup:HGroup = new HGroup();
				hgroup.percentWidth = 100;
				hgroup.percentHeight = 100;
				hgroup.horizontalAlign = "left";
				var group:Group = new Group();
				group.clipAndEnableScrolling = true;
				group.percentWidth = 100;
				group.percentHeight = 100;
				fView = new FormationsView;
				fView.percentWidth = 100;
				group.addElement(fView);
				var vscrollbar:VScrollBar = new VScrollBar();
				vscrollbar.percentHeight = 100;
				vscrollbar.viewport = group;
				hgroup.addElement(group);
				hgroup.addElement(vscrollbar);
				modelView.addElement(hgroup);
				
				var matsPanel:Panel = new Panel();
				matsPanel.title = "资源";
				matsPanel.percentHeight = 60;
				matsPanel.percentWidth = 100;
				hgroup = new HGroup();
				hgroup.percentWidth = 100;
				hgroup.percentHeight = 100;
				hgroup.horizontalAlign = "left";
				group = new Group();
				group.clipAndEnableScrolling = true;
				group.percentWidth = 100;
				group.percentHeight = 100;
				vscrollbar = new VScrollBar();
				vscrollbar.percentHeight = 100;
				vscrollbar.viewport = group;
				hgroup.addElement(group);
				hgroup.addElement(vscrollbar);
				matsPanel.addElement(hgroup);
				matsView = new MatsView;
				matsView.percentWidth = 100;
				group.addElement(matsView);
				
				_enemyEditVGroup = new VGroup();
				_enemyEditVGroup.percentHeight = 100;
				_enemyEditVGroup.percentWidth = 100;
				_enemyEditVGroup.verticalAlign = "top";
				_enemyEditVGroup.horizontalAlign = "center";
				_enemyEditVGroup.addElement(modelView);
				_enemyEditVGroup.addElement(matsPanel);
				
				changeEditType(0);
			}
			
			public function changeEditType( type:int ):void {
				accordion.selectedIndex = type;
				middleArea.removeAllElements();
				rightArea.removeAllElements();
				if (type == 0) {
					middleArea.addElement(_enemyEditView);
					rightArea.addElement(_enemyEditVGroup);
				}
				else {
					middleArea.addElement(_bgEditView);
				}
			}
			
			private function onBgDataInit(event:GameEvent):void {
				EventManager.getInstance().removeEventListener(EventType.INIT_BG_DATA_COMPLETE, onBgDataInit);
				var list:List = new List();
				list.percentWidth = 100;
				list.percentHeight = 100;
				var pathList:Array = File.desktopDirectory.resolvePath("editor").getDirectoryListing();
				var resultList:ArrayCollection = new ArrayCollection;
				for (var i:uint = 0; i < pathList.length; i++) 
				{
					var splitStr:String = Capabilities.os.substring(0, 3) == "Mac" ? "/" : "\\";
					var path:String = String(pathList[i].nativePath).split(splitStr).pop();
					if(path.split(".").pop() == "tmx")
						resultList.addItem(path);
				}
				list.dataProvider = resultList;
				bgEdit.addElement(list);
				list.addEventListener(MouseEvent.CLICK, onTMXClick);
				
				/*_bgList = new List();
				_bgList.percentWidth = 100;
				_bgList.percentHeight = 100;
				_bgList.dataProvider = BgModel.getInstance().bgNameArray;
				_bgList.addEventListener(MouseEvent.CLICK, function (e:Event):void {
					if ((e.currentTarget as List).selectedIndex >= 0) {
						_bgEditView.tmxMapView.load(BgModel.getInstance().bgDict[BgModel.getInstance().bgNameArray[(e.currentTarget as List).selectedIndex]]);
					}
				});
				bgEdit.addElement(_bgList);
				
				_bgEditView = new BgEditView();*/
			}
			
			private function onLevelClick(e:Event):void{
				/*var selectedNode:XML = (e.currentTarget as Tree).selectedItem as XML;
				var label:String=selectedNode.@label;
				Data.getInstance().currSelectedLevel = (e.currentTarget as Tree).selectedIndex;
				_enemyEditView.init(label);*/
				setCurrLevel(_tree.selectedIndex);
			}	
			
			private function setCurrLevel(index:int):void
			{
				_tree.selectedIndex = index;
				Data.getInstance().currSelectedLevel = index;
				_enemyEditView.init(Data.getInstance().displayData[index].levelName);
			}
			private function onTMXClick(e:MouseEvent):void
			{
				var file:File = File.desktopDirectory.resolvePath("editor/"+List(e.currentTarget).selectedItem);
				EditMapControl.getInstance().setMapTMX(file);
				_enemyEditView.switchMap();
			}
			
			private function onCreateNew(e:ContextMenuEvent):void
			{
				var levelLength:int = Data.getInstance().levelXML.level.length()+1;
				Data.getInstance().makeNewLevel("level"+levelLength);
				_enemyEditView.init("level"+levelLength);
			}
			
			private function onRename(e:ContextMenuEvent):void 
			{
				var window:RenamePanel = new RenamePanel;
				window.addEventListener(MsgEvent.RENAME_LEVEL, onRenameClick);
				
				PopUpManager.addPopUp(window, this, true);
				PopUpManager.centerPopUp(window);
			}
			
			private function onRenameClick(e:MsgEvent):void
			{
				var index:int = _tree.selectedIndex;
				if(index != -1)
				{
					Data.getInstance().renameLevel(index, e.hintMsg);
				}
			}
			
			private function onDelete(e:ContextMenuEvent):void
			{
				if (accordion.selectedIndex == 0) {
					Alert.okLabel = "确定";
					Alert.cancelLabel = "取消";
					var selectedNode:XML = _tree.selectedItem as XML;
					if(selectedNode)
						Alert.show("确定删除？", "tips", Alert.OK|Alert.CANCEL, this, alertClose);
					else 
						Alert.show("选择关卡");
				}
				else {
					if (_bgList.selectedIndex >= 0) {
						BgModel.getInstance().removePair(_bgList.selectedIndex);
						_bgEditView.tmxMapView.load(BgModel.getInstance().bgDict[BgModel.getInstance().bgNameArray[0]]);
					}
				}
			}
			
			private function alertClose(e:CloseEvent):void
			{
				if(e.detail == Alert.OK)
				{
					var index:int = _tree.selectedIndex
					if(index != -1)
					{
						Data.getInstance().deleteLevel(index);
						_tree.dataProvider = Data.getInstance().levelXML;
						_enemyEditView.clear();
						setCurrLevel(Math.max(index-1, 0));
					}
				}
			}
			private function saveFile(event:Event):void
			{
				_enemyEditView.save();
				Data.getInstance().saveLocal();
				Data.getInstance().exportJS();
			}
			
			private function openLevelFile(event:Event):void {
				var file:File = File.desktopDirectory;
				var tip:String = "请选择关卡配置文件";
				var filter:FileFilter = new FileFilter("JSON","*.json;*.txt");
				file.browseForOpen(tip, [filter]);
				file.addEventListener(Event.SELECT, function (e:Event):void {
					var file:File = e.target as File;
					var fileStream:FileStream = new FileStream();
					fileStream.open(file, FileMode.READ);
					var config:Object = JSON.parse(fileStream.readUTFBytes(fileStream.bytesAvailable));
					// add to Data.getInstance();
				});
			}
			
			private function openBgFile(event:Event):void {
				var browseForOpenFile:File = File.desktopDirectory;
				var tip:String = "请选择背景地图文件(.tmx)";
				var filter:FileFilter = new FileFilter("Tiled Map File","*.tmx");
				browseForOpenFile.browseForOpen(tip, [filter]);
				browseForOpenFile.addEventListener(Event.SELECT, function (e:Event):void {
					var file:File = e.target as File;
					EditMapControl.getInstance().setMapTMX(file);
					_enemyEditView.switchMap();
				});
			}
			
			private function openExcelFile(event:Event):void {
				var file:File = File.desktopDirectory;
				var tip:String = "请选择怪物Excel配置文件";
				var filter:FileFilter = new FileFilter("XLSX","*.xlsx");
				file.browseForOpen(tip, [filter]);
				file.addEventListener(Event.SELECT, function (e:Event):void {
					var file:File = e.target as File;
					ExcelReader.getInstance().initWithNativePath(file.nativePath);
				});
			}
			
			var process:NativeProcess;
			private function sendToServer(e:Event):void
			{
				if(_tree.selectedIndex == -1)
				{
					Alert.show("上传前请先选择关卡！");
					return;
				}
				
				saveFile(null);
				if (ServerManager.getInstance().uploadFilesToServer(File.desktopDirectory.resolvePath("editor/Resources"), tagInput.text)) {
//					Alert.show("上传成功！");
				}
				else {
					Alert.show("上传失败！"); 
				}
			}
			
			private function onAccordionClick(e:Event):void {
				//changeEditType((e.currentTarget as Accordion).selectedIndex);
			}
			
			public function get getInstance():MapEditor { return _instance; }
			
			private var _instance:MapEditor;
			
			private var _enemyEditView:EditView;
			private var _enemyEditVGroup:VGroup;
			private var _tree:Tree;
			
			private var _bgEditView:BgEditView;
			private var _bgList:List;
		]]>
	</fx:Script>
	
	<s:layout>
		<s:HorizontalLayout verticalAlign="middle" horizontalAlign="center"/>
	</s:layout>
	<s:VGroup height="100%" width="100%" paddingTop="5" paddingLeft="5" paddingRight="5" paddingBottom="5">
		<s:HGroup id="toolbar" width="100%" verticalAlign="middle" horizontalAlign="left">
				<s:Button id="openBtn" label="打开关卡配置" click="openLevelFile(event)"/>
				<s:Button id="saveBtn" label="保存关卡配置" click="saveFile(event)"/>
				<s:Button id="openBgBtn" label="添加背景地图" click="openBgFile(event)"/>
				<s:Button id="openExcelBtn" label="打开怪物Excel" click="openExcelFile(event)"/>
				<s:Button id="sendBtn" label="上传" click="sendToServer(event)"/>
				<s:Label text="服务器Tag" color="0x777777"/>
				<s:TextInput id="tagInput" width="100" text="zwf"/>
		</s:HGroup>
		
		<s:HGroup width="100%" height="100%" verticalAlign="middle" horizontalAlign="left">
			<s:Panel id="accordionPanel" height="100%" title="选择编辑类别">
				<mx:Accordion id="accordion" height="100%" width="100%" click="onAccordionClick(event)">
					<s:NavigatorContent width="100%" height="100%" label="怪物编辑" id="enemyEdit">
					</s:NavigatorContent> 
					<s:NavigatorContent width="100%" height="100%" label="背景编辑" id="bgEdit">
					</s:NavigatorContent> 
				</mx:Accordion>
			</s:Panel>
			
			<s:BorderContainer height="100%" width="100%">
				<mx:Canvas id="middleArea" height="100%" width="100%" clipContent="true">
				</mx:Canvas>
			</s:BorderContainer>
		</s:HGroup>
	</s:VGroup>
	
	<s:VGroup height="100%" width="280" verticalAlign="top" horizontalAlign="right" paddingTop="5" paddingLeft="5" paddingRight="5" paddingBottom="5">
		<s:BorderContainer id="rightArea" height="100%" width="100%">
		</s:BorderContainer>
	</s:VGroup>
		
</s:WindowedApplication>